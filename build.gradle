plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
}

group = project.group
version = project.version

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public"
    }
    maven {
        url = "https://repo.extendedclip.com/content/repositories/placeholderapi/"
    }
}

dependencies {
    compileOnly("io.papermc.paper:paper-api:${project.paper_api}") // Paper API

    // Dependencies
    implementation("com.tchristofferson:ConfigUpdater:${project.config_updater}")
    implementation("dev.triumphteam:triumph-gui:${project.triumph_gui}")
    implementation("me.clip:placeholderapi:${project.placeholder_api}")
    implementation("com.zaxxer:HikariCP:${project.hikari}")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    javadoc.options.encoding = 'UTF-8'
}

shadowJar {
    archiveBaseName.set(project.name)
    archiveVersion.set(project.version)
    archiveClassifier.set(null)
    archiveAppendix.set(null)
    relocate('com.tchristofferson.configupdater', 'space.bxteam.nexus.configupdater')
    relocate("dev.triumphteam.gui", "space.bxteam.nexus.gui")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    repositories {
        maven {
            name = "nexus"
            url = "https://repo.bx-team.space/releases"
            credentials(PasswordCredentials)
            authentication {
                basic(BasicAuthentication)
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = "nexus"
            version = project.version
            from components.java
        }
    }
}
